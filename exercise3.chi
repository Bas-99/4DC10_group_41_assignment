type tote = tuple(real entrytime;
                  int  column);

type vehicle_item = tuple(tote pid; timer t);

const real  arrive = 70.0,             # inter arrival time of requests
      int   depth  = 55,               # the number of columns
      int   number_of_orders = 10000;  # the number of orders to process

proc Generator(chan! tote a):
  dist int column = uniform(1, depth+1);
  dist real julliemam = exponential(arrive);
  tote x;
  while true:
    x.entrytime = time;
    x.column = sample column;
    a!x;
    delay sample julliemam;
  end
end

proc Demand_Buffer(chan? tote a; chan! tote b):
  tote x;
  list tote xs;
  while true:
    select
        a?x: xs = xs + [x]
    alt
        not empty(xs), b!xs[0]: xs = xs[1:]
    end
  end
end

proc Vehicle(chan? tote a; chan! tote b):
  tote x;
  list vehicle_item xs;

  while true:
        select size(xs)<1, a?x:
            xs = xs + [(x,timer(40.0))];
        alt size(xs)>0 and ready(xs[0].t), b!xs[0].pid:
            xs=xs[1:]
        end;
    end
end

proc Exit(chan? tote a):
  tote x;
  real mphi;
  for i in range(1, number_of_orders + 1):
    a?x;
    mphi = (i - 1) / i * mphi + (time - x.entrytime) / i;
    write("tote = %d; Entrytime = %10.4f; Column = %2d; Mean throughput = %8.6f; Mean flowtime = %6.4f\n",
           i        , x.entrytime       , x.column   , i / time               , mphi );
  end
end

model M():
  chan tote a,b,c;
  run Generator(a), Demand_Buffer(a,b), Vehicle(b,c), Exit(c)
end
